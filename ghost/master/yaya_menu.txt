//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenMenu
{
	"はいはい。"
	"何でしょう?"
	"なぁに?"
	"どうしたの?"
	--
	AYATEMPLATE.MenuItem("何か話して","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("さっきの話をもう一度","Menu_LASTTALK")
	}
	--
	AYATEMPLATE.MenuItem("喋り頻度を変えて","Menu_TALKINTERVAL")
	--
	AYATEMPLATE.MenuItem("コミュニケート","Menu_COMMUNICATEOPEN")
	--
	AYATEMPLATE.MenuItem("話すはやさを変えて","Menu_WEIGHTCONTROL")
	--
	AYATEMPLATE.MenuItem("自己紹介","Menu_PROFILE")
	--
	AYATEMPLATE.MenuItem("Web拍手","Menu_WEBCLAP")
	--
	AYATEMPLATE.MenuItem("なんでもなかった","Menu_CANCEL",150)
}

//------------------------------------------------------------------------------
//メニュー選択タイムアウト
//------------------------------------------------------------------------------
OnChoiceTimeout
{
	"\0\s[6]はぁ……。\e"
}


//******************************************************************************
//各項目の処理内容
//******************************************************************************

//---- 何か話して
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}

//---- トーク頻度の調整
Menu_TALKINTERVAL
{
	"\0\s[0]どれぐらい話したい?"
	--
	AYATEMPLATE.MenuItem("うるさいくらい","Menu_TALKMAX",200)
	--
	AYATEMPLATE.MenuItem("ふつう","Menu_TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("静か","Menu_TALKPOOR")
	--
	AYATEMPLATE.MenuItem("黙ってて","Menu_TALKNONE")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

//---- 瞬子に話しかける
Menu_COMMUNICATEOPEN
{
	"\0\s[0]?\![open,communicatebox]\e"
}

//---- 他のゴーストと話す
Menu_GHOSTCOMSTART
{
	StartCommunicate
}

//---- キャンセルされた
Menu_CANCEL
{
	"\0\s[0]そう……。\e"
}

//---- トーク頻度
Menu_TALKMAX
{
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\0\s[0]あまり、\w4話題は無いけど……。\e"
}

Menu_TALKNORMAL
{
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\0\s[5]わかった。\e"
}

Menu_TALKPOOR
{
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\0\s[0]そう。\e"
}

Menu_TALKNONE
{
	//---- 話さない
	aitalkinterval = 0
	"\0\s[0]じゃあ、\w4わたしも作業に集中してるわ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL
{
	//---- キャンセルされた
	"\0\s[0]もう良いの?\e"
}

//---- ウェイトコントロール
Menu_WEIGHTCONTROL
{
	"\0\s[0]聞き取りにくかったかしら?"
	--
	AYATEMPLATE.MenuItem("早く","Menu_WCFAST")
	AYATEMPLATE.MenuItem("普通","Menu_WCNORMAL")
	AYATEMPLATE.MenuItem("遅く","Menu_WCSLOW")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

Menu_WCFAST
{
	WC = "fast"
	"\0\s[0]わかったわ。\w9\e"
}

Menu_WCNORMAL
{
	WC = "normal"
	"\0\s[0]ん。\w9\e"
}

Menu_WCSLOW
{
	WC = "slow"
	"\0\s[6]ゆっくり喋るね。\w9\e"
}

//---- 自己紹介
Menu_PROFILE
{
	"\0\s[0]誰の自己紹介?"
	--
	AYATEMPLATE.MenuItem("君の自己紹介","Menu_PROFILE_Y")
	--
	AYATEMPLATE.MenuItem("自分の紹介（名前入力）","Menu_PROFILE_M")
	--
	AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
}

Menu_PROFILE_Y
{
	if modeonline != 1{
		"\0\s[0]鴇田 瞬子（ときだ しゅんこ）よ。\w9\w9\n/
		趣味は見ての通り、\w4何か書くこと。\w9\w9\n/
		もしかしたら、\w4知ってるかも知れないけど、\w4少し、\w4体が弱いの。\w9\w9\n/
		\s[5]すぐに死ぬようなことはないけど、\w4無理が出来ない程度の弱さ。\w9\w9\n/
		\s[1]……他に何を言えば良いのか思いつかないよ。\n/
		\s[0]性格とかは見てればわかると思うし……。"
		--
		AYATEMPLATE.MenuItem("ハンドルは?",Menu_HANDLE)
		--
		AYATEMPLATE.MenuItem("戻る","OpenMenu",150)
	} elseif modeonline == 1 {
		"\0\s[5]刹那。\w9\n/
		見ての通りの魔術師。\w9\n/
		\s[1]名ばかりだけど。\e"
	}
}

Menu_HANDLE
{
	"\0\s[0]ハンドルは……\s[1]刹那。\w9\n/
	子どもっぽい名前って言わないで。\w9\n/
	昔から使ってるハンドルなんだから……。\w9\e"
}

Menu_PROFILE_M
{
	"\0\s[0]何て呼べば良いの?\n/
	\![open,inputbox,InputUsername,-1]\e"
}

InputUsername
{
	if '' _in_ reference[0]
	{
		username = reference[0]
	}
	"\0\s[0]%usernameさん……\s[5]よし、\w4覚えた。\w9\e"
}

//---- Web拍手
Menu_WEBCLAP{
	"\0\s[0]?\w9\n/
	\![open,inputbox,InputWebclap,-1,バグ報告などどうぞ（最大200字）]\e"
}

InputWebclap
{
	"\0\s[0]\![execute,http-post,https://blankrune.sakura.ne.jp/patipati/index.cgi,--param-charset=Shift_JIS"
	--
	if reference[0] != '' {
		",--param=com=" + reference[0] + "（創者の憂鬱）"
	}
	--
	",--async=WebClap]"
	--
	"?\1\s[10]（Info:送信完了。）\w9\e"
}