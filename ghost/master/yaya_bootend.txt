//******************************************************************************
// YAYA　ゴーストテンプレート
// 起動/終了トーク辞書
//******************************************************************************

//******************************************************************************
//初回起動 OnFirstBoot
//******************************************************************************

OnFirstBoot
{
	//---- 変数の初期化
	username         = "お隣"
	teachusername    = 0	// ユーザー名覚え用
	WC               = "normal" //ウェイト
	shellname        = "tenebres"

	//---- Vanishカウントを見て、真の初回か否かを判定する
	if reference[0] == 0
	{
		//---- ほんとうに初めての起動
		"\0\s[5]あ、回覧板持ってきてくれたの?\w9\n/
		\s[1]ごめんなさい、\w4つい書くのに夢中になってたから……。\w9\w9\n/
		\s[0]わざわざ、\w4ありがとう。\w9\w9\n/
		\s[5]ついでに上がってく?\w9\w9\n/
		\0\s[6]何もないけど。\w9\e"
	}
	else
	{
		//---- 再インストール後の初回起動
		""
		--
		"\0\s[0]また、\w4来たんだ。\w9\n/
		\s[5]いらっしゃい。\w9\e"
	}
}


//******************************************************************************
//通常の起動 OnBoot
//******************************************************************************

OnBoot
{
	//---- まず双方のサーフィスを表示
	"\0\s[0]\1\s[10]"
	--
	//---- 起動時間を確認してあいさつを変化
	_timeslot = GetTimeSlot
	
	case _timeslot{
	when "朝"
	{
		"\0\s[0]おはよう。\w9\e"
		"\0\s[5]おはよう。\w9\n/
		\s[0]今日も来たのね。\w9\e"
		"\0\s[0]%usernameさん、\w4おはよう。\w9\n/
		\s[5]よく眠れた?\e"
		"\0\s[5]おはようございます。\w9\n/
		\s[1]今日も一日、\w4よろしくお願いします……\s[5]なんてね。\e"
	}
	when "昼"
	{
		"\0\s[5]こんにちは、\w4かな。\w9\e"
		"\0\s[0]ゆっくりするのに良い時間ね。\w9\e"
		"\0\s[5]こんにちは、\w4ね。\e"
	}
	when "日中"
	{
		"\0\s[0]いらっしゃい。\w9\e"
		"\0\s[5]こんにちは。\w9\e"
		"\0\s[5]こんにちは。\w9\n/
		今日も何も無いけれど、\w4どうぞ。\e"
	}
	when "夜"
	{
		"\0\s[0]こんばんは。\w9\e"
		"\0\s[0]いらっしゃい、\w4夕飯は食べた?\w9\n/
		\s[5]もし良かったら一緒にどう?\w9\w9\n/
		\s[1]一人で食べるのは味気ないから。\w9\e"
		"\0\s[5]こんばんは、\w4%usernameさん。\e"
	}
	others
	{
		"\0\s[0]こんばんは、\w4こんな夜遅くにどうしたの?\w9\e"
		"\0\s[0]いらっしゃい。\w9\n/
		そろそろ、\w4寝る時間だと思うけど……。\w9\e"
		"\0\s[0]こんばんは……\s[2000]と言うには遅いかな?\e"
		"\0\s[0]こんばんは。\w9\n/
		\s[5]こんな時間に来るとは思ってなかったわ。\e"
	}
	}//caseの終わり
}

//------------------------------------------------------------------------------
//朝/昼/夜などをおおざっぱに調べる関数
//------------------------------------------------------------------------------
GetTimeSlot
{
	case hour{
	when 22,23,0,1,2,3 {'深夜'}
	when 4,5,6,7,8,9,10,11 {'朝'}
	when 12,13 {'昼'}
	when 14,15,16,17 {'日中'}
	when 18,19,20,21 {'夜'}
	}//caseの終わり
}


//******************************************************************************
//終了 OnClose
//******************************************************************************

OnClose
{
	"\1\s[10]"
	--
	//この部分には\eつけないでね！
	_timeslot = GetTimeSlot
	if _timeslot == "朝"
	{
		//---- 朝に終了
		"\0\s[0]帰るの?\w9\n/
		気をつけてね。"
	}
	elseif _timeslot == "昼" || _timeslot == "日中"
	{
		"\0\s[0]またね。"
		"\0\s[0]さよなら。"
		"\0\s[0]またね、\w4%username"
	}
	else
	{
		//---- 夜/深夜に終了
		"\0\s[5]おやすみなさい。\w9\n/
		\s[6]良い夜を。"
		"\0\s[0]良い夢を。"
	}
	--
	"\w9\w9\-\e"
}


//******************************************************************************
//最小化からの復帰 OnWindowStateRestore
//******************************************************************************

OnWindowStateRestore
{
	"\0\s[1]慌てて出て行くから驚いちゃったよ。\w9\e"
	"\0\s[0]どうしたの?\w9\e"
}
